export import "std/hash_map.znn";
export import "std/box.znn";
export import "std/pair.znn";

export import "ui/ui.znn";
import "ui/data.znn";

export import "highlighter.znn";
import "buffer.znn";
import "editor_ui.znn";
import "dialogs.znn";
import "lsp_client_map.znn";
import "scrolling_list_ui.znn";

void setPrompt(ProgramState mutable* programState, string question, box<invoke<void>> callback_yes) {
  const uiData = &programState^.uiData;
  uiData^.insert("prompt_callback_yes", [move(callback_yes), uiData] {
    callback_yes^();
    uiData^.erase(&"prompt_callback_yes");
    uiData^.erase(&"prompt_callback_no");
    uiData^.erase(&"prompt_text");
  }.box_as<UICallback>().box_as<any>());
  uiData^.insert("prompt_callback_no", [uiData, programState] {
    programState^.toDo.push_back([uiData] {
      uiData^.erase(&"prompt_callback_yes");
      uiData^.erase(&"prompt_callback_no");
      uiData^.erase(&"prompt_text");
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  uiData^.insert("prompt_text", question.box_as<any>());
}

bool isContentUnsaved(ProgramState* programState) {
  for (elem : programState^.editorData)
    if (!programState^.paths[elem.index] || elem^.buffer.isModified())
      return true;
  return false;
}

template <T>
struct SearchInfo {
  string? entry = "";
  vector<T> results = vector<T>();
  vector<T> origResults = vector<T>();
};

template <T>
SearchInfo() {
  return SearchInfo<T>::SearchInfo("", vector<T>(), vector<T>());
}

template <T> requires copy<T>
SearchInfo(string? text, vector<T> results) {
  return SearchInfo<T>::SearchInfo(text, results.copy(), results.copy());
}

template <T> requires copy<T>
SearchInfo(string? text, vector<T> results, vector<T> origResults) {
  return SearchInfo<T>::SearchInfo(move(text), move(results), move(origResults));
}

template <T> requires copy<T>
SearchInfo<T> copy(SearchInfo<T>*) = default;

template <T>
void handleTextEntry(SearchInfo<T> mutable* info, TextCallbackParam param) {
  if (info^.entry)
    switch (param) {
      case (text) {
        *info^.entry += text;
      }
      case (backspace) {
        if (!info^.entry^.empty())
          *info^.entry = info^.entry^.substring(0, info^.entry^.size() - 1);
      }
    }
}
union FileSearchResult {
  string path;
  int line;
};

FileSearchResult copy(FileSearchResult*) = default;

struct LocationResult {
  string text;
  int line;
  int bufferIndex;
};

LocationResult copy(LocationResult*) = default;

export union OverlayInfo {
  string search;
  SearchInfo<FileSearchResult> file;
  SearchInfo<LocationResult> location;
  SearchInfo<LspCompletionOption> completion;
  void switchBuffer;
};

export struct ProgramState {
  UIRecord uiData = UIRecord();
  UIState uiState = UIState();
  int currentBuffer = 0;
  vector<EditorUIData> editorData = vector<EditorUIData>();
  vector<int> bufferOrder = vector<int>();
  vector<string?> paths;
  vector<string> searchHistory = vector<string>();
  int? searchHistoryIndex = null;
  OverlayInfo? overlayInfo = null;
  SearchInfo<LocationResult>? previousLocationSearch = null;
  vector<box<invoke<void>>> toDo = vector<box<invoke<void>>>();
  Highlighter highlighter;
  LspSignature? signature = null;
  LspHoverResult? hover = null;
  string basePath;
};

void addCursorHistory(ProgramState mutable* programState) {
  const editorData = programState^.getEditorData();
  programState^.getEditorState()^.addCursorHistory(editorData^.cursor, programState^.currentBuffer);
}

EditorUIData mutable*? getEditorData(ProgramState mutable* programState, string path) {
  for (p : programState^.paths)
    if (*p == path)
      return &programState^.editorData[p.index];
  return null;
}

EditorUIData mutable* getEditorData(ProgramState mutable* programState) {
  return &programState^.editorData[programState^.currentBuffer];
}

EditorUIState mutable* getEditorState(ProgramState mutable* programState) {
  return programState^.uiState.get_state<EditorUIState>();
}

void resetScrollingUIState(ProgramState mutable* programState) {
  programState^.resetScrollingUIState(0);
}

void resetScrollingUIState(ProgramState mutable* programState, int index) {
  const s = programState^.uiState.get_state<ScrollingListUIState>();
  s^.index = index;
  s^.scrollPos = 0;
  printf("Resetting state\n");
}

export void updateOtherLspContent(ProgramState mutable* programState, LspClientMap mutable* lspClientMap) {
  mutable int? index = null;
  for (path : programState^.paths)
    if (!!*path && !programState^.editorData[path.index].buffer.lspUpdated &&
        !!lspClientMap^.getClient(**path) &&
        (!index || programState^.editorData[path.index].buffer.origLastModified ?? 0.as_long() >
        programState^.editorData[*index].buffer.origLastModified ?? 0.as_long()))
      index = path.index;
  if (index) {
    const buffer = &programState^.editorData[*index].buffer;
    buffer^.lspUpdated = true;
    const path = programState^.paths[*index];
    lspClientMap^.getClient(*path)^^.sendFileChangedNotification(*path, &buffer^.joinLines());
  }
}

bool posInRange(vec2 pos, vec2 start, vec2 end) {
  return (pos.y > start.y || (pos.y == start.y && pos.x >= start.x)) &&
      (pos.y < end.y || (pos.y == end.y && pos.x <= end.x));
}

export bool updateCurrentLspContent(ProgramState mutable* programState, LspClientMap mutable* lspClientMap) {
  const data = programState^.getEditorData();
  mutable ret = false;
  if (const path = programState^.paths[data^.buffer.index]) {
    if (const lspClient = lspClientMap^.getClient(*path)) {
      if (!data^.buffer.lspUpdated) {
        printf("Updating content\n");
        data^.buffer.lspUpdated = true;
        lspClient^^.sendFileChangedNotification(*path, &data^.buffer.joinLines());
        ret = ret || true;
      }
      if (!programState^.signature || programState^.signature^.position != data^.cursor ||
          programState^.signature^.file != *path) {
        if (lspClient^^.sendSignatureRequest(*path, data^.cursor)) {
          printf("Signature request\n");
          ret = ret || true;
        }
      }
      const state = programState^.getEditorState();
      if (!programState^.hover || programState^.hover^.file != *path ||
          !posInRange(data^.cursor, programState^.hover^.startPos, programState^.hover^.endPos)) {
        if (lspClient^^.sendHoverRequest(*path, data^.cursor)) {
          printf("Hover request\n");
          ret = ret || true;
        }
      }
    }
  }
  return ret;
}

export void handleLspMessages(ProgramState mutable* programState, LspClientMap mutable* lspClientMap) {
  for (lspClient : lspClientMap^.clients)
    if (const msg = lspClient^.getMessage())
      switch (*msg) {
        case (diagnostic) {
          if (const data = programState^.getEditorData(diagnostic.file))
            data^^.diagnostics = diagnostic.locations.copy();
        }
        case (referencesResult) {
          if (referencesResult.size() == 1) {
            const result = &referencesResult[0];
            if (!programState^.getEditorData(result^.file)) {
              programState^.openFileAndGoTo(result^.file);
              programState^.goToPositionNoHistory(result^.file, result^.position);
            } else
              programState^.goToPosition(result^.file, result^.position);
          } else
          if (!referencesResult.empty()) {
            programState^.previousLocationSearch = SearchInfo(null,
                referencesResult.transform([programState](LspDefinitionResult* r) {
                  if (!programState^.getEditorData(r^.file))
                    discard programState^.openFile(r^.file);
                  const data = programState^.getEditorData(r^.file);
                  return LocationResult(
                      data^^.buffer.text[r^.position.y],
                      r^.position.y + 1,
                      data^^.buffer.index
                  );
                }));
            programState^.overlayInfo = OverlayInfo::location(programState^.previousLocationSearch^.copy());
            programState^.resetScrollingUIState();
          }
        }
        case (completionResult) {
          if (completionResult.options.size() == 1) {
            programState^.applyCompletion(&completionResult.options[0], 0);
          } else
          if (completionResult.options.size() > 1) {
            programState^.overlayInfo = OverlayInfo::completion(SearchInfo("", completionResult.options.copy()));
            programState^.resetScrollingUIState();
          }
        }
        case (signature) {
          programState^.signature = signature.copy();
        }
        case (hover) {
          programState^.hover = hover.copy();
        }
      }
}

void doSearchRepeat(ProgramState mutable* programState, bool reverse) {
  const state = programState^.uiState.get_state<EditorUIState>();
  const editorData = programState^.getEditorData();
  if (const text = getSelectedTextInLine(editorData)) {
    programState^.addCursorHistory();
    state^.searchedText = SearchedTextInfo(*text, reverse);
    if (programState^.searchHistory.empty() || *programState^.searchHistory.back() != *text) {
      programState^.searchHistory.push_back(*text);
      programState^.searchHistoryIndex = null;
    }
  } else
  if (!programState^.searchHistory.empty()) {
    programState^.addCursorHistory();
    state^.searchedText = SearchedTextInfo(*programState^.searchHistory.back(), reverse);
  }
}

export void goToPosition(ProgramState mutable* programState, string path, vec2 position) {
  programState^.addCursorHistory();
  programState^.goToPositionNoHistory(path, position);
}

export void goToPositionNoHistory(ProgramState mutable* programState, string path, vec2 position) {
  for (p : programState^.paths)
    if (*p == path) {
      programState^.switchToBuffer(p.index);
      programState^.getEditorData()^.selectionStart = null;
      programState^.getEditorState()^.searchedPos = position;
    }
}

bool isCppFile(string path) {
  return path.ends_with(&".cpp") || path.ends_with(&".h");
}

export void closeBuffer(ProgramState mutable* programState, int index) {
  programState^.paths.remove(index);
  programState^.editorData.remove(index);
  programState^.bufferOrder.remove(*programState^.bufferOrder.find(&index));
  for (elem : programState^.bufferOrder)
    if (*elem > index)
      --*elem;
  if (programState^.currentBuffer > index)
    --programState^.currentBuffer;
  else
    programState^.currentBuffer = programState^.bufferOrder[0];
  for (i = index; i < programState^.editorData.size(); ++i)
    --programState^.editorData[i].buffer.index;
  const state = programState^.getEditorState();
  for (elem : state^.cursorHistory) {
    if (elem^.bufferIndex > index)
      --elem^.bufferIndex;
  }
  state^.cursorHistory = state^.cursorHistory.filter([&](CursorData* elem) { return elem^.bufferIndex != index;});
}

void openFileAndGoTo(ProgramState mutable* programState, string path) {
  programState^.addCursorHistory();
  programState^.currentBuffer = programState^.openFile(move(path));
}

int openFile(ProgramState mutable* programState, string path) {
  if (const index = programState^.paths.find(&path))
    return *index;
  else {
    programState^.paths.push_back(path);
    programState^.bufferOrder.insert(0, programState^.paths.size() - 1);
    programState^.editorData.push_back(EditorUIData(
      Buffer(programState^.paths.size() - 1, *get_modification_time(path), *open_file_r(path)^.read_content()),
      vec2(0, 0),
      vec2(0, 0),
      null,
      &programState^.highlighter,
      vector<LspDiagnosticLocation>()
    ));
    return programState^.paths.size() - 1;
  }
}

export void addPermanentCallbacks(ProgramState mutable* programState, bool mutable* exit,
    LspClientMap mutable* lspClientMap) {
  programState^.uiData.insert("exit_callback", [programState, exit] {
    if (programState^.overlayInfo) {
      programState^.overlayInfo = null;
      return;
    } else
      setPrompt(programState, "Exit?", [exit] { *exit = true; }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("switch_buffer_callback", [programState] {
    programState^.overlayInfo = OverlayInfo::switchBuffer();
    programState^.resetScrollingUIState(1);
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("open_file_callback", [programState] {
    programState^.toDo.push_back([programState] {
      for (file : getOpenFiles("Open file(s):"))
        programState^.openFileAndGoTo(*file);
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("new_file_callback", [programState] {
    programState^.toDo.push_back([programState] {
      const string? p = null;
      programState^.paths.push_back(move(p));
      programState^.bufferOrder.insert(0, programState^.paths.size() - 1);
      programState^.editorData.push_back(EditorUIData(
        Buffer(programState^.paths.size() - 1, null, ""),
        vec2(0, 0),
        vec2(0, 0),
        null,
        &programState^.highlighter,
        vector<LspDiagnosticLocation>()
      ));
      programState^.currentBuffer = programState^.editorData.size() - 1;
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("close_file_callback", [programState, exit
  ] {
    if (programState^.paths.size() == 1)
      setPrompt(programState, "Exit?", [exit] { *exit = true; }.box_as<invoke<void>>());
    else
    if (programState^.getEditorData()^.buffer.isModified())
      setPrompt(programState, "Discard unsaved changes?", [programState] {
          programState^.closeBuffer(programState^.currentBuffer);
        }.box_as<invoke<void>>());
    else
      programState^.toDo.push_back([programState] {
        programState^.closeBuffer(programState^.currentBuffer);
      }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("save_callback", [programState] {
    for (elem : programState^.editorData) {
      const path = &programState^.paths[elem.index];
      if (elem^.buffer.isModified() || !*path) {
        if (!*path) {
          *path = getSavePath("Save file:");
        }
        if (*path) {
          if (const file = open_file_w(**path)) {
            file^.printf(elem^.buffer.joinLines());
            elem^.buffer.origText = elem^.buffer.text.copy();
          }
          elem^.buffer.origLastModified = *get_modification_time(**path);
        }
      }
    }
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("search_callback", [programState] {
    programState^.overlayInfo = OverlayInfo::search("");
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("file_search_callback", [programState] {
    programState^.overlayInfo = OverlayInfo::file(SearchInfo<FileSearchResult>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("location_search_callback", [programState] {
    if (const text = getSelectedTextInLine(programState^.getEditorData()))
      programState^.doLocationSearch(*text);
    else if (!programState^.overlayInfo && !!programState^.previousLocationSearch)
      programState^.overlayInfo = OverlayInfo::location(programState^.previousLocationSearch^.copy());
    else
      programState^.doLocationSearch("");
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("search_repeat_callback", [programState] {
    doSearchRepeat(programState, false);
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("search_reverse_repeat_callback", [programState] {
    doSearchRepeat(programState, true);
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("go_to_definition_callback", [programState, lspClientMap] {
    if (const path = programState^.paths[programState^.currentBuffer]) {
      const data = programState^.getEditorData();
      if (const client = lspClientMap^.getClient(*path))
        client^^.sendDefinitionRequest(*path, data^.cursor);
    }
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("find_references_callback", [programState, lspClientMap] {
    if (const path = programState^.paths[programState^.currentBuffer]) {
      const data = programState^.getEditorData();
      if (const client = lspClientMap^.getClient(*path))
        client^^.sendReferencesRequest(*path, data^.cursor);
    }
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("completion_callback", [programState, lspClientMap] {
    if (const path = programState^.paths[programState^.currentBuffer]) {
      const data = programState^.getEditorData();
      if (const client = lspClientMap^.getClient(*path)) {
        printf("Completion request\n");
        discard programState^.updateCurrentLspContent(lspClientMap);
        client^^.sendCompletionRequest(*path, data^.cursor);
      }
    }
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("navigate_back_callback", [programState] {
    programState^.toDo.push_back([programState] {
      const state = programState^.getEditorState();
      if (!state^.cursorHistory.empty()) {
        const data = programState^.getEditorData();
        const popped = state^.cursorHistory.pop_back();
        state^.cursorForward.push_back(CursorData(data^.cursor, programState^.currentBuffer));
        programState^.currentBuffer = popped.bufferIndex;
        state^.searchedPos = popped.pos;
      }
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("navigate_forward_callback", [programState] {
    programState^.toDo.push_back([programState] {
      const state = programState^.getEditorState();
      if (!state^.cursorForward.empty()) {
        const data = programState^.getEditorData();
        const popped = state^.cursorForward.pop_back();
        state^.cursorHistory.push_back(CursorData(data^.cursor, programState^.currentBuffer));
        programState^.currentBuffer = popped.bufferIndex;
        state^.searchedPos = popped.pos;
      }
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  programState^.uiData.insert("switch_to_header_callback", [programState] {
    const curPath = programState^.paths[programState^.getEditorData()^.buffer.index];
    if (!curPath)
      return;
    const suffix1 = ".cpp";
    const suffix2 = ".h";
    const toPath = [&] -> string? {
      if (curPath^.ends_with(&suffix1))
        return curPath^.prefix(curPath^.size() - suffix1.size()) + suffix2;
      if (curPath^.ends_with(&suffix2))
        return curPath^.prefix(curPath^.size() - suffix2.size()) + suffix1;
      return null;
    }();
    if (toPath)
      if (const ind = programState^.paths.find(&*toPath)) {
        programState^.addCursorHistory();
        programState^.switchToBuffer(*ind);
      }
  }.box_as<UICallback>().box_as<any>());
}

void doLocationSearch(ProgramState mutable* programState, string text) {
  mutable locationResults = vector<LocationResult>();
  if (text.size() > 1)
    for (elem : programState^.editorData) {
      for (line : elem^.buffer.text)
        if (!!line^.find(&text)) {
          locationResults.push_back(LocationResult(
            .text = *line,
            .line = line.index + 1,
            .bufferIndex = elem.index
          ));
          if (locationResults.size() > 100)
            break;
        }
    }
  locationResults.sort(&[&](LocationResult* r1, LocationResult* r2) {
    const isStructDef = [&](LocationResult* r) {
      return r^.text.ends_with(&"\{") && (!!r^.text.find("struct " + text) || !!r^.text.find("class " + text));
    };
    const isStructDef1 = isStructDef(r1);
    const isStructDef2 = isStructDef(r2);
    if (isStructDef1 != isStructDef2)
      return isStructDef1;
    const isMethod1 = !!r1^.text.find("::");
    const isMethod2 = !!r2^.text.find("::");
    if (isMethod1 != isMethod2)
      return isMethod1;
    const isHeader1 = programState^.getBufferName(r1^.bufferIndex).ends_with(&".h");
    const isHeader2 = programState^.getBufferName(r2^.bufferIndex).ends_with(&".h");
    if (isHeader1 != isHeader2)
      return isHeader1;
    return false;
  });
  programState^.previousLocationSearch = SearchInfo(text, move(locationResults));
  programState^.overlayInfo = OverlayInfo::location(programState^.previousLocationSearch^.copy());
  programState^.resetScrollingUIState();
}

void doFileSearch(ProgramState mutable* programState, string text) {
  mutable fileResults = vector<FileSearchResult>();
  if (text.size() > 1)
    for (elem : programState^.paths)
      if (!!*elem && !!elem^^.find(&text)) {
        fileResults.push_back(FileSearchResult::path(**elem));
        if (fileResults.size() > 100)
          break;
      }
  if (const n = text.to_int())
    fileResults.push_back(FileSearchResult::line(*n));
  programState^.overlayInfo = OverlayInfo::file(SearchInfo(text, move(fileResults)));
  programState^.resetScrollingUIState();
}

void searchAndReplace(ProgramState mutable* programState, string pattern, string replace) {
  const data = programState^.getEditorData();
  data^.buffer.addHistory(data^.cursor, data^.cursor);
  if (data^.selectionStart) {
    for (ind : range(min(data^.selectionStart^.y, data^.cursor.y), max(data^.selectionStart^.y, data^.cursor.y) + 1))
      if (const value = data^.buffer.text[ind].replace(&pattern, &replace))
        data^.buffer.text[ind] = *value;
  } else
    for (line : data^.buffer.text)
      if (const value = line^.replace(&pattern, &replace))
        *line = *value;
}

pair<string, string>? breakIntoPatternAndReplace(string text) {
  for (i : range(0, text.size())) {
    if (text[i] == '\\') {
      ++i;
      if (i >= text.size())
        break;
      continue;
    }
    if (text[i] == '/')
      return pair(text.substring(0, i), text.substring(i + 1, text.size() - i - 1));
  }
  return null;
}

export void addOverlayCallbacks(ProgramState mutable* programState, UICanvas* canvas) {
  programState^.uiData.erase(&"location_results_up");
  programState^.uiData.erase(&"location_results_down");
  programState^.uiData.erase(&"location_results_confirm");
  programState^.uiData.erase(&"search_entry");
  programState^.uiData.erase(&"search_confirm");
  programState^.uiData.erase(&"search_content");
  programState^.uiData.erase(&"search_paste_callback");
  programState^.uiData.erase(&"search_history_up_callback");
  programState^.uiData.erase(&"search_history_down_callback");  
  programState^.uiData.erase(&"location_results");
  programState^.uiData.erase(&"completion_results");
  programState^.uiData.erase(&"file_results");
  programState^.uiData.erase(&"signatures");
  programState^.uiData.erase(&"hoverText");
  programState^.uiData.erase(&"next_buffer_path");
  const editorData = programState^.getEditorData();
  const editorState = programState^.getEditorState();
  if (!!programState^.hover && !programState^.hover^.content.empty() &&
      posInRange(editorData^.cursor, programState^.hover^.startPos, programState^.hover^.endPos) && 
      programState^.paths[programState^.currentBuffer] == programState^.hover^.file) {
    const v = programState^.hover^.content.transform(
        [](string* s) { return s^.box_as<any>(); });
    programState^.uiData.insert("hoverText", move(v).box_as<any>());
  }
  if (!!programState^.signature && !programState^.signature^.elems.empty() &&
      programState^.signature^.position == editorData^.cursor && 
      programState^.paths[programState^.currentBuffer] == programState^.signature^.file) {
    const v = programState^.signature^.elems.transform(
        [](string* s) { return s^.box_as<any>(); });
    programState^.uiData.insert("signatures", move(v).box_as<any>());
  }
  if (programState^.overlayInfo)
    switch (*programState^.overlayInfo) {
      case (search) {
        programState^.uiData.insert("search_paste_callback", [programState, &search, canvas] {
          if (programState^.searchHistoryIndex)
            search = programState^.searchHistory[*programState^.searchHistoryIndex];
          search = search + canvas^.getClipboard();
        }.box_as<UICallback>().box_as<any>());
        programState^.uiData.insert("search_history_up_callback", [programState, &search] {
          if (!programState^.searchHistory.empty()) {
            programState^.searchHistoryIndex = max(0,
                -1 + programState^.searchHistoryIndex ?? programState^.searchHistory.size());
          }
        }.box_as<UICallback>().box_as<any>());
        programState^.uiData.insert("search_history_down_callback", [programState, &search] {
          if (!programState^.searchHistory.empty() && !!programState^.searchHistoryIndex) {
            if (*programState^.searchHistoryIndex >= programState^.searchHistory.size() - 1)
              programState^.searchHistoryIndex = null;
            else
              programState^.searchHistoryIndex = *programState^.searchHistoryIndex + 1;
          }
        }.box_as<UICallback>().box_as<any>());
        programState^.uiData.insert("search_confirm", [programState, &search]() {
          if (programState^.searchHistoryIndex)
            search = programState^.searchHistory[*programState^.searchHistoryIndex];
          if (programState^.searchHistory.empty() || *programState^.searchHistory.back() != search)
            programState^.searchHistory.push_back(search);
          programState^.searchHistoryIndex = null;
          programState^.addCursorHistory();
          if (const p = breakIntoPatternAndReplace(search))
            programState^.searchAndReplace(p^.first, p^.second);
          else {
            programState^.getEditorState()^.searchedText = SearchedTextInfo(search, false);
          }
          programState^.overlayInfo = null;
        }.box_as<UICallback>().box_as<any>());
        programState^.uiData.insert("search_entry", [programState, &search](TextCallbackParam param) {
          if (programState^.searchHistoryIndex)
            search = programState^.searchHistory[*programState^.searchHistoryIndex];
          programState^.searchHistoryIndex = null;
          switch (param) {
            case (text) {
              search += text;
            }
            case (backspace) {
              if (!search.empty())
                search = search.substring(0, search.size() - 1);
            }
          }
        }.box_as<UITextCallback>().box_as<any>());
        programState^.uiData.insert("search_content", box_as<any>(!!programState^.searchHistoryIndex
            ? programState^.searchHistory[*programState^.searchHistoryIndex]
            : search));
      }
      case (file) {
        programState^.uiData.insert("search_entry", [programState, &file](TextCallbackParam param) {
          file.handleTextEntry(move(param));
          programState^.doFileSearch(*file.entry);
          programState^.resetScrollingUIState();
        }.box_as<UITextCallback>().box_as<any>());
        programState^.uiData.insert("search_content", box_as<any>(*file.entry));
        mutable v = vector<box<any>>();
        for (elem : file.results) {
          mutable r = UIRecord();
          switch (*elem) {
            case (path) { r.insert("text", path.box_as<any>()); }
            case (line) { r.insert("text", "Line: {line}".box_as<any>()); }
          }
          v.push_back(move(r).box_as<any>());
        }
        const confirmCallback = [programState, &file] (int index) {
          if (!file.results.empty())
            programState^.toDo.push_back([programState, &file, index] {
              programState^.addCursorHistory();
              switch (file.results[index]) {
                case (path) {
                  programState^.switchToBuffer(*programState^.paths.find(&path));
                }
                case (line) {
                  programState^.getEditorState()^.searchedPos = vec2(0, line - 1);
                }
              }
              programState^.overlayInfo = null;
            }.box_as<invoke<void>>());
        };
        programState^.uiData.insert("file_results", ScrollingListUIData(
            move(v),
            move(confirmCallback).box_as<invoke<void, int>>()
        ).box_as<any>());
      }
      case (location) {
        programState^.uiData.insert("search_entry", [programState, &location](TextCallbackParam param) {
          if (location.entry) {
            location.handleTextEntry(move(param));
            programState^.doLocationSearch(*location.entry);
            programState^.resetScrollingUIState();
          }
        }.box_as<UITextCallback>().box_as<any>());
        if (location.entry)
          programState^.uiData.insert("search_content", box_as<any>(*location.entry));
        mutable v = vector<box<any>>();
        for (elem : location.results) {
          mutable r = UIRecord();
          r.insert("text", elem^.text.box_as<any>());
          r.insert("path", programState^.getBufferName(elem^.bufferIndex).box_as<any>());
          r.insert("line", elem^.line.to_string().box_as<any>());
          v.push_back(move(r).box_as<any>());
        }
        const confirmCallback = [programState, &location] (int index) {
          if (!location.results.empty())
            programState^.toDo.push_back([programState, &location, index] {
              programState^.addCursorHistory();
              const result = &location.results[index];
              programState^.switchToBuffer(result^.bufferIndex);
              programState^.getEditorData()^.selectionStart = null;
              programState^.getEditorState()^.searchedPos = vec2(0, result^.line - 1);
            }.box_as<invoke<void>>());
        };
        programState^.uiData.insert("location_results", ScrollingListUIData(
            move(v),
            move(confirmCallback).box_as<invoke<void, int>>()
        ).box_as<any>());
      }
      case (completion) {
        programState^.uiData.insert("search_entry", [programState, &completion](TextCallbackParam param) {
          const data = programState^.getEditorData();
          switch (param) {
            case (text) {
              data^.cursor = data^.buffer.onText(param.text, data^.cursor);
            }
            case (backspace) {
              data^.cursor = data^.buffer.onBackspace(data^.cursor, 1);
              if (completion.entry^.empty()) {
                programState^.overlayInfo = null;
                return;
              }
            }
          }
          completion.handleTextEntry(move(param));
          completion.results = completion.origResults.filter(
            [&](LspCompletionOption* option) {
              return option^.text.substring(option^.numErased).starts_with(&*completion.entry);
          });
          programState^.resetScrollingUIState();
        }.box_as<UITextCallback>().box_as<any>());
        mutable v = vector<box<any>>();
        for (elem : completion.results) {
          mutable r = UIRecord();
          r.insert("text", elem^.label.box_as<any>());
          v.push_back(move(r).box_as<any>());
        }
        const confirmCallback = [programState, &completion] (int index) {
          programState^.applyCompletion(&completion.results[index], completion.entry^.size());
        };
        programState^.uiData.insert("completion_results", ScrollingListUIData(
            move(v),
            move(confirmCallback).box_as<invoke<void, int>>()
        ).box_as<any>());
      }
      case (switchBuffer) {
        mutable v = vector<box<any>>();
        for (i : range(0, programState^.paths.size())) {
          mutable r = UIRecord();
          const bufferIndex = programState^.bufferOrder[i];
          mutable bufferName = programState^.getBufferName(bufferIndex);
          if (bufferName.starts_with(&programState^.basePath)) {
            bufferName = bufferName.substring(programState^.basePath.size());
            if (bufferName.starts_with(&"/"))
              bufferName = bufferName.substring(1);
          }
          r.insert("text", bufferName.box_as<any>());
          if (!programState^.editorData[bufferIndex].diagnostics.empty())
            r.insert("errors", 1.box_as<any>());
          else if (programState^.editorData[bufferIndex].buffer.lspUpdated)
            r.insert("updated", 1.box_as<any>());
          v.push_back(move(r).box_as<any>());
        }
        const callback = [programState] (int index) {
          programState^.addCursorHistory();
          programState^.switchToBuffer(programState^.bufferOrder[index]);
          programState^.overlayInfo = null;
        };
        programState^.uiData.insert("next_buffer_path", ScrollingListUIData(
            move(v),
            move(callback).box_as<invoke<void, int>>()
        ).box_as<any>());
      }
    }
}

void applyCompletion(ProgramState mutable* programState, LspCompletionOption* result, int numEntered) {
  const data = programState^.getEditorData();
  data^.cursor = data^.buffer.erase(data^.cursor - vec2(result^.numErased + numEntered, 0), data^.cursor);
  data^.cursor = data^.buffer.onText(result^.text, data^.cursor);
  if (result^.kind == 2 || result^.kind == 3 || result^.kind == 4) {
    data^.cursor = data^.buffer.onText("()", data^.cursor);
    --data^.cursor.x;
  }
  programState^.overlayInfo = null;
}

void switchToBuffer(ProgramState mutable* programState, int index) {
  programState^.currentBuffer = index;
  programState^.bufferOrder.remove(*programState^.bufferOrder.find(&index));
  programState^.bufferOrder.insert(0, index);
}

export string getBufferName(ProgramState* programState, int bufferIndex) {
  return programState^.paths[bufferIndex] ?? "[unsaved]";
}

export void addConditionalCallbacks(ProgramState mutable* programState) {
  if (!isContentUnsaved(programState))
    programState^.uiData.insert("no_changes", 1.box_as<any>());
  else
    programState^.uiData.erase(&"no_changes");
  programState^.uiData.insert("editor", box_as<any>(programState^.getEditorData()));
  programState^.uiData.insert("buffer_name", (programState^.getBufferName(programState^.currentBuffer) +
      " {programState^.getEditorData()^.cursor + vec2(1, 1)}").box_as<any>());
}
