import "sdl.znn";
import "sdl_surface.znn";
import "sdl_canvas.znn";
import "ui.znn";
import "std/io.znn";
import "ui_reader.znn";
import "sdl_ttf.znn";
import "ui_data.znn";
import "std/enum_set.znn";
import "std/enum.znn";
import "std/time.znn";
import "std/flags.znn";
import "std/panic.znn";
import "pretty_input.znn";
import "editor_ui.znn";
import "std/vector.znn";
import "sdl_event.znn";
import "scrollbar_ui.znn";
import "dialogs.znn";

box<UIElement>!string readUIFrom(string path) {
  const content = *open_file_r(path)^.read_content();
  mutable stream = try create_input_stream(content);
  stream.add_concept_mapping<UIElement, EditorUI>("editor");
  stream.add_concept_mapping<UIElement, ScrollbarUI>("scrollbar");
  stream.register_ui_elems();
  return stream.read_data<box<UIElement>>();
}

export bool round_brackets(pretty_dummy_struct<EditorUI>) {
  return true;
}

export bool round_brackets(pretty_dummy_struct<ScrollbarUI>) {
  return true;
}

struct Flags {
  bool noPanicHandler;
  string? font;
  vector<string> all;
};

template <Types...> requires copy<Types>...
UIRecord createRecord(string[] names, Types... values) {
  mutable ret = UIRecord();
  static for (i = 0; i < countof(values); ++i) {
    ret.insert(names[i], values...[i].copy().box_as<any>());
  }
  return move(ret);
}

void setPrompt(UIRecord mutable* uiData, string question, box<invoke<void>> callback_yes,
    box<invoke<void>> callback_no) {
  uiData->insert("prompt_callback_yes", [move(callback_yes), uiData] {
    callback_yes^();
    uiData->erase(&"prompt_callback_yes");
    uiData->erase(&"prompt_callback_no");
    uiData->erase(&"prompt_text");
  }.box_as<UICallback>().box_as<any>());
  uiData->insert("prompt_callback_no", [move(callback_no), uiData] {
    callback_no^();
    uiData->erase(&"prompt_callback_yes");
    uiData->erase(&"prompt_callback_no");
    uiData->erase(&"prompt_text");
  }.box_as<UICallback>().box_as<any>());
  uiData->insert("prompt_text", question.box_as<any>());
}

bool isContentUpdated(vector<EditorUIData>* data) {
  for (elem : *data)
    if (elem^.buffer.isModified())
      return false;
  return true;
}

struct AllState {
  SDLCanvas canvas;
  rectangle uiBounds;
  bool exit;
  UIRecord uiData;
  bool flushEvents;
  SDLEventsState eventsState;
  UIState uiState;
  int currentBuffer;
  int? nextBuffer;
  vector<EditorUIData> editorData;
  vector<int> bufferOrder;
  vector<string> paths;
  string? lastSearchedText;
  string? searchEntry;
  bool lineSearch;
  vector<box<invoke<void>>> toDo;
};

AllState(SDLCanvas canvas, rectangle windowBounds, vector<string> paths) {
  return AllState::AllState(
    .canvas = move(canvas),
    .uiBounds = move(windowBounds),
    .exit = false,
    .uiData = UIRecord(),
    .flushEvents = false,
    .eventsState = SDLEventsState(),
    .uiState = UIState(),
    .currentBuffer = 0,
    .nextBuffer = null,
    .editorData = vector<EditorUIData>(),
    .bufferOrder = vector<int>(),
    .paths = move(paths),
    .lastSearchedText = null,
    .searchEntry = null,
    .lineSearch = false,
    .toDo = vector<box<invoke<void>>>()
  );
}

void addPermanentCallbacks(AllState mutable* allState) {
  allState^.uiData.insert("exit_callback", [allState] {
    if (allState^.searchEntry) {
      allState^.searchEntry = null;
      return;
    }
    setPrompt(&allState^.uiData, "Exit?",
        [allState] { allState^.exit = true; }.box_as<invoke<void>>(),
        [allState] {
          allState^.flushEvents = true;
          discard allState^.eventsState.getEvents();
        }.box_as<invoke<void>>()
    );
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("open_file_callback", [allState] {
    allState^.toDo.push_back([allState] {
      for (file : getOpenFiles("Open file(s):")) {
        if (!allState^.paths.find(&*file)) {
          allState^.paths.push_back(*file);
          allState^.bufferOrder.insert(0, allState^.paths.size() - 1);
          allState^.editorData.push_back(EditorUIData(
            Buffer(*open_file_r(*file)^.read_content()),
            vec2(0, 0),
            0,
            null
          ));
          allState^.currentBuffer = allState^.editorData.size() - 1;
        }
      }
    }.box_as<invoke<void>>());
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("switch_buffer_callback1", [allState] {
    allState^.nextBuffer = (allState^.nextBuffer ?? 0 + 1) % allState^.editorData.size();
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("save_callback", [allState] {
    for (elem : allState^.editorData)
      if (const file = open_file_w(allState^.paths[elem.index])) {
        file->printf(elem->buffer.joinLines());
        elem->buffer.origText = elem->buffer.text.copy();
      }
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("search_callback", [allState] {
      allState^.searchEntry = "";
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("line_search_callback", [allState] {
      allState^.searchEntry = "";
      allState^.lineSearch = true;
  }.box_as<UICallback>().box_as<any>());
  allState^.uiData.insert("search_repeat_callback", [allState] {
    const state = allState^.uiState.get_state<EditorUIState>();
    const editorData = &allState^.editorData[allState^.currentBuffer];
    if (!!editorData^.selectionStart && editorData^.selectionStart^.y == editorData^.cursor.y) {
      mutable begin = editorData^.selectionStart^.x;
      mutable end = editorData^.cursor.x;
      if (begin > end)
        swap(&begin, &end);
      state^.searchedText = editorData^.buffer.text[editorData^.cursor.y]
          .substring(begin, end - begin);
      allState^.lastSearchedText = state^.searchedText;
    } else
    if (allState^.lastSearchedText)
      state^.searchedText = *allState^.lastSearchedText;
  }.box_as<UICallback>().box_as<any>());
}

void addConditionalCallbacks(AllState mutable* allState) {
  if (allState^.nextBuffer) {
    mutable v = vector<box<any>>();
    for (i : range(0, allState^.paths.size())) {
      mutable r = UIRecord();
      r.insert("text", allState^.paths[allState^.bufferOrder[i]].box_as<any>());
      if (i == *allState^.nextBuffer)
        r.insert("selected", 1.box_as<any>());
      v.push_back(move(r).box_as<any>());
    }
    allState^.uiData.insert("next_buffer_path", move(v).box_as<any>());
    allState^.uiData.insert("switch_buffer_callback2", [allState] {
        allState^.currentBuffer = allState^.bufferOrder[*allState^.nextBuffer];
        allState^.bufferOrder.remove(*allState^.bufferOrder.find(&allState^.currentBuffer));
        allState^.bufferOrder.insert(0, allState^.currentBuffer);
        allState^.nextBuffer = null;
        }.box_as<UICallback>().box_as<any>());
    allState^.uiData.insert("switch_buffer_callback_up", [allState] {
        allState^.nextBuffer = (*allState^.nextBuffer - 1 + allState^.paths.size())
            % allState^.paths.size();
        }.box_as<UICallback>().box_as<any>());
    allState^.uiData.insert("switch_buffer_callback_down", [allState] {
        allState^.nextBuffer = (*allState^.nextBuffer + 1) % allState^.paths.size();
        }.box_as<UICallback>().box_as<any>());
  } else {
    allState^.uiData.erase(&"switch_buffer_callback_up");
    allState^.uiData.erase(&"switch_buffer_callback_down");
    allState^.uiData.erase(&"switch_buffer_callback2");
    allState^.uiData.erase(&"next_buffer_path");
  }
  if (isContentUpdated(&allState^.editorData))
    allState^.uiData.insert("no_changes", 1.box_as<any>());
  else
    allState^.uiData.erase(&"no_changes");
  allState^.uiData.insert("editor", box_as<any>(&allState^.editorData[allState^.currentBuffer]));
  allState^.uiData.insert("buffer_name", (allState^.paths[allState^.currentBuffer] +
      " {allState^.editorData[allState^.currentBuffer].cursor + vec2(1, 1)}").box_as<any>());
  if (allState^.searchEntry) {
    allState^.uiData.insert("search_paste_callback", [allState] {
      allState^.searchEntry = *allState^.searchEntry + allState^.canvas.getClipboard();
    }.box_as<UICallback>().box_as<any>());
    allState^.uiData.insert("search_confirm", [allState]() {
      if (allState^.lineSearch) {
        if (const n = allState^.searchEntry^.to_int())
          allState^.uiState.get_state<EditorUIState>()->searchedLine = n;
      } else {
        allState^.lastSearchedText = allState^.searchEntry;
        allState^.uiState.get_state<EditorUIState>()->searchedText = allState^.searchEntry;
      }
      allState^.searchEntry = null;
    }.box_as<UICallback>().box_as<any>());
    allState^.uiData.insert("search_entry", [allState](TextCallbackParam param) {
      switch (param) {
        case (text) {
          *allState^.searchEntry += text;
        }
        case (backspace) {
          if (!allState^.searchEntry^.empty())
            allState^.searchEntry = allState^.searchEntry^.substring(0, allState^.searchEntry^.size() - 1);
        }
      }
    }.box_as<UITextCallback>().box_as<any>());
    allState^.uiData.insert("search_content", box_as<any>(*allState^.searchEntry));
  } else {
    allState^.uiData.erase(&"search_entry");
    allState^.uiData.erase(&"search_confirm");
    allState^.uiData.erase(&"search_content");
    allState^.uiData.erase(&"search_paste_callback");
  }
}

int main(string[] args) {
  const flags = *read_flags<Flags>(args);
  const panic_handler = [](string s) { printf("{s}\n"); exit(0); };
  if (!flags.noPanicHandler)
    set_panic_handler(&panic_handler);
  *sdl_init();
  mutable ui = *readUIFrom("ui.txt");
  const window = *sdl_create_window(vec2(1280, 720), "ZIM");
  mutable font = *window.loadSDLFont(flags.font ?? "font.ttf", 10, 100);
  const getCanvas = [&] {
    const textTexture = *window.make_target_texture(vec2(100, 100));
    const gameSprites = hash_map<string, sdl_texture>();
    return SDLCanvas(&window, &font, move(gameSprites), move(textTexture));
  };
  mutable allState = AllState(
      getCanvas(),
      rectangle(vec2(0, 0), window.size()),
      flags.all.transform([](string* path) { return get_canonical_path(*path); })
  );
  for (path : allState.paths) {
    allState.bufferOrder.push_back(path.index);
    allState.editorData.push_back(EditorUIData(
      Buffer(*open_file_r(*path)^.read_content()),
      vec2(0, 0),
      0,
      null
    ));
  }
  allState.addPermanentCallbacks();
  while (!allState.exit) {
    while (!allState.toDo.empty()) {
      allState.toDo.back()^^();
      discard allState.toDo.pop_back();
    }
    allState.addConditionalCallbacks();
    const context = UIContext(&allState.canvas, &allState.uiData, &allState.uiState, 0.1);
    for (event : allState.eventsState.getEvents()) {
      if (allState.exit || allState.flushEvents) {
        allState.flushEvents = false;
        break;
      }
      switch (*event) {
        case (windowResized) {
          allState.uiBounds = rectangle(vec2(0, 0), windowResized);
        }
        case (quit) { return 0; }
        case (key) {
          if (key.key == key_type::F1 && key.type == key_event_type::DOWN) {
            allState.canvas = getCanvas();
            if (const res = readUIFrom("ui.txt"))
              ui = *move(res);
            else
              printf(res.get_error());
            } else
            discard ui^.onKeyEvent(context, &allState.uiBounds, &key);
        }
        case (mouse) {
          discard ui^.onMouseEvent(context, &allState.uiBounds, &mouse);
        }
        case (text) {
          discard ui^.onTextEvent(context, &allState.uiBounds, text);
        }
        default {}
      }
    }
    ui^.render(context, &allState.uiBounds);
    window.update();
  }
  return 0;
}
