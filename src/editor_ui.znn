export import "ui.znn";
import "std/vector.znn";
import "std/io.znn";
export import "buffer.znn";

export [@ui_element]
struct EditorUI {
  int fontSize;
  int tabWidth;
  color selectionColor;
  color textColor;
  int mouseWheelStep;
};

export struct EditorUIState {
  bool mouseSelection;
};

export struct EditorUIData {
  Buffer buffer;
  vec2 cursor;
  int scrollPos;
  vec2? selectionStart;
};

vec2 getVisibleCursor(EditorUIData* data, Buffer* buffer) {
  mutable ret = data->cursor;
  ret.x = min(ret.x, data^.buffer.text[data->cursor.y].size());
  return move(ret);
}

export EditorUIState() {
  return EditorUIState::EditorUIState(false);
}

void renderSelection(EditorUI* editor, UIContext context, rectangle* r, vec2 textSize) {
  const data = **context.data^.cast_to<EditorUIData mutable*>();
  if (!!data->selectionStart && *data->selectionStart != data->cursor) {
    mutable startPos = *data->selectionStart;
    mutable endPos = data->getVisibleCursor(&data->buffer);
    if ((startPos.y == endPos.y && startPos.x > endPos.x) || startPos.y > endPos.y)
      swap(&startPos, &endPos);
    const origStartLine = startPos.y;
    const renderLine = [&] (int line, int px, int kx) {
      const pos = r->v1 + vec2(px, (line - data->scrollPos)).pairwise_mult(textSize);
      const pos2 = r->v1 + vec2(kx, (line + 1 - data->scrollPos)).pairwise_mult(textSize);
      context.canvas->fillRectangle(&rectangle(pos, pos2), &editor->selectionColor);
    };
    while (startPos.y <= endPos.y) {
      renderLine(startPos.y, startPos.y == origStartLine ? startPos.x : 0,
          startPos.y == endPos.y ? endPos.x : data^.buffer.text[startPos.y].size());
      ++startPos.y;
    }
  }
}

export void render(EditorUI* editor, UIContext context, rectangle* r) {
  const data = **context.data^.cast_to<EditorUIData mutable*>();
  const textSize = context.canvas->getTextSize(" ", editor->fontSize);
  while (data^.scrollPos > 0 && r^.v1.y + textSize.y * (data^.buffer.text.size() - data->scrollPos) < r^.v2.y)
    --data->scrollPos;
  renderSelection(editor, context, r, textSize);
  for (line : range(data->scrollPos, data^.buffer.text.size())) {
    const pos = r->v1 + vec2(0, (line - data->scrollPos) * textSize.y);
    if (pos.y > r->v2.y)
      break;
    context.canvas->drawText(pos, data^.buffer.text[line], editor->fontSize, &editor->textColor);
  }
  const cursorPos = r->v1 + textSize.pairwise_mult(data->getVisibleCursor(&data^.buffer) - vec2(0, data->scrollPos));
  context.canvas->drawLine(cursorPos, cursorPos + vec2(0, textSize.y), &editor->textColor);
}

export bool onKeyEvent(EditorUI* editor, UIContext context, rectangle* r, key_event* event) {
  const data = **context.data^.cast_to<EditorUIData mutable*>();
  if (event^.type != key_event_type::DOWN)
    return false;
  const textSize = context.canvas->getTextSize(" ", editor->fontSize);
  if (event^.key == key_type::UP || event^.key == key_type::DOWN || event^.key == key_type::LEFT
      || event^.key == key_type::RIGHT || event^.key == key_type::PAGE_UP || event^.key == key_type::PAGE_DOWN
      || event^.key == key_type::HOME || event^.key == key_type::END) {
    if (event->modifiers.contains(key_event_modifier::LSHIFT)) {
      if (!data->selectionStart)
        data->selectionStart = data->getVisibleCursor(&data^.buffer);
    } else
      data->selectionStart = null;
  }
  switch (event^.key) {
    case (ENTER) {
      data->cursor = data^.buffer.onEnter(data->getVisibleCursor(&data^.buffer));
    }
    case (BACKSPACE) {
      if (data->selectionStart) {
        data->cursor = data^.buffer.erase(*data->selectionStart, data->getVisibleCursor(&data^.buffer));
        data->selectionStart = null;
      } else
        data->cursor = data^.buffer.onBackspace(data->getVisibleCursor(&data^.buffer));
    }
    case (DELETE) {
      if (data->selectionStart) {
        data->cursor = data^.buffer.erase(*data->selectionStart, data->getVisibleCursor(&data^.buffer));
        data->selectionStart = null;
      } else
        data->cursor = data^.buffer.onDelete(data->getVisibleCursor(&data^.buffer));
    }
    case (HOME) {
      data->cursor.x = 0;
    }
    case (END) {
      data->cursor.x = data^.buffer.text[data->cursor.y].size();
    }
    case (UP) {
      data->cursor.y = max(0, data->cursor.y - 1);
    }
    case (DOWN) {
      data->cursor.y = min(data^.buffer.text.size() - 1, data->cursor.y + 1);
    }
    case (PAGE_UP) {
      data->cursor.y = max(0, data->cursor.y - r->height() / textSize.y);
    }
    case (PAGE_DOWN) {
      data->cursor.y = min(data^.buffer.text.size() - 1, data->cursor.y + r->height() / textSize.y);
    }
    case (TAB) {
      if (data->selectionStart) {
        data^.buffer.changeIndent(*data->selectionStart, data^.cursor,
            event^.modifiers.contains(key_event_modifier::LSHIFT) ? -editor^.tabWidth : editor^.tabWidth);
      } else
      if (event^.modifiers.contains(key_event_modifier::LSHIFT)) 
        data^.buffer.changeIndent(data^.cursor, data^.cursor, -editor^.tabWidth);
      else {
        mutable tabs = "";
        for (i : range(0, abs(editor^.tabWidth)))
          tabs += " ";
        discard editor->onTextEvent(context, r, tabs);
      }
    }
    case (LEFT) {
      const cursor = data->getVisibleCursor(&data^.buffer);
      if (cursor.x == 0) {
        if (cursor.y > 0) {
          --data->cursor.y;
          data->cursor.x = data^.buffer.text[data->cursor.y].size();
        }
      } else
        data->cursor.x = cursor.x - 1;
    }
    case (RIGHT) {
      if (data->cursor.x >= data^.buffer.text[data->cursor.y].size()) {
        if (data->cursor.y < data^.buffer.text.size() - 1) {
          ++data->cursor.y;
          data->cursor.x = 0;
        }
      } else
        ++data->cursor.x;
    }
    case (C) {
      if (event^.modifiers.contains(key_event_modifier::LCTRL) && !!data->selectionStart) {
        context.canvas->setClipboard(data^.buffer.getString(*data->selectionStart, data->getVisibleCursor(&data^.buffer)));
        return true;
      }
      return false;
    }
    case (X) {
      if (event^.modifiers.contains(key_event_modifier::LCTRL) && !!data->selectionStart) {
        context.canvas->setClipboard(data^.buffer.getString(*data->selectionStart, data->getVisibleCursor(&data^.buffer)));
        data->cursor = data^.buffer.erase(*data->selectionStart, data->getVisibleCursor(&data^.buffer));
        return true;
      }
      return false;
    }
    case (V) {
      if (event^.modifiers.contains(key_event_modifier::LCTRL)) {
        const lines = breakLines(context.canvas->getClipboard());
        for (it : lines) {
          discard editor->onTextEvent(context, r, *it);
          if (it.index < lines.size() - 1)
            data->cursor = data^.buffer.onEnter(data->getVisibleCursor(&data^.buffer));
        }
        return true;
      }
      return false;
    }
    case (Z) {
      if (event^.modifiers.contains(key_event_modifier::LCTRL)) {
        if (const pos = data^.buffer.undo(data->getVisibleCursor(&data^.buffer)))
          data->cursor = *pos;
        return true;
      }
      return false;
    }
    case (Y) {
      if (event^.modifiers.contains(key_event_modifier::LCTRL)) {
        if (const pos = data^.buffer.redo(data->getVisibleCursor(&data^.buffer)))
          data->cursor = *pos;
        return true;
      }
      return false;
    }
    default {
      return false;
    }
  }
  while (true) {
    const cursorPos = r->v1.y + textSize.y * (data->getVisibleCursor(&data^.buffer).y - data->scrollPos);
    if (data->scrollPos > 0 && cursorPos < r->v1.y + 20)
      --data->scrollPos;
    else if (cursorPos > r->v2.y - textSize.y)
      ++data->scrollPos;
    else
      break;
  }
  return true;
}

export bool onMouseEvent(EditorUI* editor, UIContext context, rectangle* r, mouse_event* event) {
  const state = context.state^.get_state<EditorUIState>();
  const data = **context.data^.cast_to<EditorUIData mutable*>();
  switch (event->type) {
    case (MOUSE_MOVEMENT) {
      if (state->mouseSelection) {
        const textSize = context.canvas->getTextSize(" ", editor->fontSize);
        data->cursor = (event->pos - r->v1).pairwise_div(textSize) + vec2(0, data->scrollPos);
        data->cursor.y = min(data->buffer.text.size() - 1, data->cursor.y);
        data->cursor.x = min(data->buffer.text[data->cursor.y].size(), data->cursor.x);
      }
      return true;
    }
    case (LEFT_RELEASED) {
      if (state->mouseSelection) {
        if (data->selectionStart == data->cursor)
          data->selectionStart = null;
        state->mouseSelection = false;
        return true;
      }
    }
    case (LEFT) {
      const textSize = context.canvas->getTextSize(" ", editor->fontSize);
      data->cursor = (event->pos - r->v1).pairwise_div(textSize) + vec2(0, data->scrollPos);
      data->cursor.y = min(data->buffer.text.size() - 1, data->cursor.y);
      data->cursor.x = min(data->buffer.text[data->cursor.y].size(), data->cursor.x);
      data->selectionStart = data->cursor;
      state->mouseSelection = true;
      return true;
    }
    case (WHEEL_UP) {
      data->scrollPos = max(0, data->scrollPos - editor->mouseWheelStep);
    }
    case (WHEEL_DOWN) {
      data->scrollPos += editor->mouseWheelStep;
    }
    default {
      return false;
    }
  }
  return true;
}

export bool onTextEvent(EditorUI*, UIContext context, rectangle*, string text) {
  const data = **context.data^.cast_to<EditorUIData mutable*>();
  if (data->selectionStart) {
    data->cursor = data^.buffer.erase(*data->selectionStart, data->getVisibleCursor(&data^.buffer));
    data->selectionStart = null;
  }
  data->cursor = data^.buffer.onText(text, data->getVisibleCursor(&data^.buffer));
  return true;
}

export void destruct(EditorUI*) {
}

